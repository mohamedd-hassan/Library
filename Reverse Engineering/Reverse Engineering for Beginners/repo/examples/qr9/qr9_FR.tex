\mysection{\q{QR9}: algorithme cryptographique amateur inspiré du Rubik's cube}

Parfois, les systèmes cryptographiques amateurs sont assez bizarres.

On m'a demandé une fois de rétro-ingénieurer un algorithme cryptographique amateur
d'un utilitaire de chiffrement, dont le code source avait été perdu\footnote{J'ai
aussi reçu la permission du client de publier les détails de l'algorithme.}.

Voici le listing exporté depuis \IDA de l'utilitaire de chiffrement d'origine:


Tous les noms de fonction et de label ont été donné par moi lors de l'analyse.

Commençons depuis le haut. Voici une fonction qui prend deux noms de fichier et un
mot de passe.

\begin{lstlisting}[style=customasmx86]
.text:00541320 ; int \_\_cdecl crypt\_file(char *Str, char *Filename, int password)
.text:00541320 crypt_file      proc near
.text:00541320
.text:00541320 Str             = dword ptr  4
.text:00541320 Filename        = dword ptr  8
.text:00541320 password        = dword ptr  0Ch
.text:00541320
\end{lstlisting}

Ouvrir le fichier et signaler si une erreur survient:

\begin{lstlisting}[style=customasmx86]
.text:00541320                 mov     eax, [esp+Str]
.text:00541324                 push    ebp
.text:00541325                 push    offset Mode     ; "rb"
.text:0054132A                 push    eax             ; Str
.text:0054132B                 call    _fopen          ; ouvrir le fichier
.text:00541330                 mov     ebp, eax
.text:00541332                 add     esp, 8
.text:00541335                 test    ebp, ebp
.text:00541337                 jnz     short loc_541348
.text:00541339                 push    offset Format   ; "Cannot open input file!\\n"
.text:0054133E                 call    _printf
.text:00541343                 add     esp, 4
.text:00541346                 pop     ebp
.text:00541347                 retn
.text:00541348
.text:00541348 loc_541348:
\end{lstlisting}

\myindex{\CStandardLibrary!fseek()}
\myindex{\CStandardLibrary!ftell()}
Obtenir la taille du fichier via \TT{fseek()}/\TT{ftell()}:

\lstinputlisting[style=customasmx86]{examples/qr9/1_FR}

Ce morceau de code calcule la taille du fichier aligné sur une limite de 64-octet.
Ceci car l'algorithme cryptographique travaille seulement avec des blocs de 64 octets.
L'opération est assez directe: diviser la taille du fichier par 64, ignorer le reste
et ajouter 1, puis, multiplier par 64.
Le code suivant enlève le reste comme si la valeur avait déjà été divisée par 64
et ajoute 64.

\lstinputlisting[style=customasmx86]{examples/qr9/2_FR}

Alloue le buffer avec une taille alignée:

\begin{lstlisting}[style=customasmx86]
.text:00541373                 push    esi             ; Size
.text:00541374                 call    _malloc
\end{lstlisting}

\myindex{\CStandardLibrary!calloc()}
Appelle memset(), p. ex., efface le buffer alloué\footnote{malloc() + memset() pourraient
être remplacés par calloc().}.

\lstinputlisting[style=customasmx86]{examples/qr9/3_FR}

Lit le fichier via la fonction C standard \TT{fread()}.

\begin{lstlisting}[style=customasmx86]
.text:00541392                 mov     eax, [esp+38h+Str]
.text:00541396                 push    eax             ; ElementSize
.text:00541397                 push    ebx             ; DstBuf
.text:00541398                 call    _fread          ; lire le fichier
.text:0054139D                 push    ebp             ; File
.text:0054139E                 call    _fclose
\end{lstlisting}

Appelle \TT{crypt()}. Cette fonction prend un buffer, sa taille (alignée) et une chaîne
mot de passe.

\begin{lstlisting}[style=customasmx86]
.text:005413A3                 mov     ecx, [esp+44h+password]
.text:005413A7                 push    ecx             ; mot de passe
.text:005413A8                 push    esi             ; aligner la taille
.text:005413A9                 push    ebx             ; buffer
.text:005413AA                 call    crypt           ; chiffrer
\end{lstlisting}

Crée le fichier de sortie. À propos, le développeur a oublié de vérifier s'il a été
créé correctement!

\begin{lstlisting}[style=customasmx86]
.text:005413AF                 mov     edx, [esp+50h+Filename]
.text:005413B3                 add     esp, 40h
.text:005413B6                 push    offset aWb      ; "wb"
.text:005413BB                 push    edx             ; Filename
.text:005413BC                 call    _fopen
.text:005413C1                 mov     edi, eax
\end{lstlisting}

Le handle du fichier nouvellement créé est maintenant dans le registre \EDI. Écrit
la signature \q{QR9}.

\begin{lstlisting}[style=customasmx86]
.text:005413C3                 push    edi             ; File
.text:005413C4                 push    1               ; Count
.text:005413C6                 push    3               ; Size
.text:005413C8                 push    offset aQr9     ; "QR9"
.text:005413CD                 call    _fwrite         ; écrire la signature du fichier
\end{lstlisting}

Écrit la taille réelle du fichier (non alignée):

\begin{lstlisting}[style=customasmx86]
.text:005413D2                 push    edi             ; File
.text:005413D3                 push    1               ; Count
.text:005413D5                 lea     eax, [esp+30h+Str]
.text:005413D9                 push    4               ; Size
.text:005413DB                 push    eax             ; Str
.text:005413DC                 call    _fwrite         ; écrire la taille du fichier original
\end{lstlisting}

Écrit le buffer chiffré:

\begin{lstlisting}[style=customasmx86]
.text:005413E1                 push    edi             ; File
.text:005413E2                 push    1               ; Count
.text:005413E4                 push    esi             ; Size
.text:005413E5                 push    ebx             ; Str
.text:005413E6                 call    _fwrite         ; écrire le fichier chiffré
\end{lstlisting}

Ferme le fichier et libère le buffer alloué:

\begin{lstlisting}[style=customasmx86]
.text:005413EB                 push    edi             ; File
.text:005413EC                 call    _fclose
.text:005413F1                 push    ebx             ; Memory
.text:005413F2                 call    _free
.text:005413F7                 add     esp, 40h
.text:005413FA                 pop     edi
.text:005413FB                 pop     esi
.text:005413FC                 pop     ebx
.text:005413FD                 pop     ebp
.text:005413FE                 retn
.text:005413FE crypt_file      endp
\end{lstlisting}

Voici le code C reconstruit:

\begin{lstlisting}[style=customc]
void crypt_file(char *fin, char* fout, char *pw)
{
	FILE *f;
	int flen, flen_aligned;
	BYTE *buf;

	f=fopen(fin, "rb");
	
	if (f==NULL)
	{
		printf ("Cannot open input file!\n");
		return;
	};

	fseek (f, 0, SEEK_FRD);
	flen=ftell (f);
	fseek (f, 0, SEEK_SET);

	flen_aligned=(flen&0xFFFFFFC0)+0x40;

	buf=(BYTE*)malloc (flen\_aligned);
	memset (buf, 0, flen\_aligned);

	fread (buf, flen, 1, f);

	fclose (f);

	crypt (buf, flen\_aligned, pw);
	
	f=fopen(fout, "wb");

	fwrite ("QR9", 3, 1, f);
	fwrite (&flen, 4, 1, f);
	fwrite (buf, flen\_aligned, 1, f);

	fclose (f);

	free (buf);
};
\end{lstlisting}

La procédure de déchiffrement est presque la même:

\begin{lstlisting}[style=customasmx86]
.text:00541400 ; int \_\_cdecl decrypt\_file(char *Filename, int, void *Src)
.text:00541400 decrypt_file    proc near
.text:00541400
.text:00541400 Filename        = dword ptr  4
.text:00541400 arg_4           = dword ptr  8
.text:00541400 Src             = dword ptr  0Ch
.text:00541400
.text:00541400                 mov     eax, [esp+Filename]
.text:00541404                 push    ebx
.text:00541405                 push    ebp
.text:00541406                 push    esi
.text:00541407                 push    edi
.text:00541408                 push    offset aRb      ; "rb"
.text:0054140D                 push    eax             ; Filename
.text:0054140E                 call    _fopen
.text:00541413                 mov     esi, eax
.text:00541415                 add     esp, 8
.text:00541418                 test    esi, esi
.text:0054141A                 jnz     short loc_54142E
.text:0054141C                 push    offset aCannotOpenIn_0 ; "Cannot open input file!\\n"
.text:00541421                 call    _printf
.text:00541426                 add     esp, 4
.text:00541429                 pop     edi
.text:0054142A                 pop     esi
.text:0054142B                 pop     ebp
.text:0054142C                 pop     ebx
.text:0054142D                 retn
.text:0054142E
.text:0054142E loc_54142E:
.text:0054142E                 push    2               ; Origin
.text:00541430                 push    0               ; Offset
.text:00541432                 push    esi             ; File
.text:00541433                 call    _fseek
.text:00541438                 push    esi             ; File
.text:00541439                 call    _ftell
.text:0054143E                 push    0               ; Origin
.text:00541440                 push    0               ; Offset
.text:00541442                 push    esi             ; File
.text:00541443                 mov     ebp, eax
.text:00541445                 call    _fseek
.text:0054144A                 push    ebp             ; Size
.text:0054144B                 call    _malloc
.text:00541450                 push    esi             ; File
.text:00541451                 mov     ebx, eax
.text:00541453                 push    1               ; Count
.text:00541455                 push    ebp             ; ElementSize
.text:00541456                 push    ebx             ; DstBuf
.text:00541457                 call    _fread
.text:0054145C                 push    esi             ; File
.text:0054145D                 call    _fclose
\end{lstlisting}

Vérifie la signature (3 premier octets):

\begin{lstlisting}[style=customasmx86]
.text:00541462                 add     esp, 34h
.text:00541465                 mov     ecx, 3
.text:0054146A                 mov     edi, offset aQr9_0 ; "QR9"
.text:0054146F                 mov     esi, ebx
.text:00541471                 xor     edx, edx
.text:00541473                 repe cmpsb
.text:00541475                 jz      short loc_541489
\end{lstlisting}

Renvoie une erreur si la signature est absente:

\begin{lstlisting}[style=customasmx86]
.text:00541477                 push    offset aFileIsNotCrypt ; "File is not encrypted!\\n"
.text:0054147C                 call    _printf
.text:00541481                 add     esp, 4
.text:00541484                 pop     edi
.text:00541485                 pop     esi
.text:00541486                 pop     ebp
.text:00541487                 pop     ebx
.text:00541488                 retn
.text:00541489
.text:00541489 loc_541489:
\end{lstlisting}

Appelle \TT{decrypt()}.

\begin{lstlisting}[style=customasmx86]
.text:00541489                 mov     eax, [esp+10h+Src]
.text:0054148D                 mov     edi, [ebx+3]
.text:00541490                 add     ebp, 0FFFFFFF9h
.text:00541493                 lea     esi, [ebx+7]
.text:00541496                 push    eax             ; Src
.text:00541497                 push    ebp             ; int
.text:00541498                 push    esi             ; int
.text:00541499                 call    decrypt
.text:0054149E                 mov     ecx, [esp+1Ch+arg_4]
.text:005414A2                 push    offset aWb_0    ; "wb"
.text:005414A7                 push    ecx             ; Filename
.text:005414A8                 call    _fopen
.text:005414AD                 mov     ebp, eax
.text:005414AF                 push    ebp             ; File
.text:005414B0                 push    1               ; Count
.text:005414B2                 push    edi             ; Size
.text:005414B3                 push    esi             ; Str
.text:005414B4                 call    _fwrite
.text:005414B9                 push    ebp             ; File
.text:005414BA                 call    _fclose
.text:005414BF                 push    ebx             ; Memory
.text:005414C0                 call    _free
.text:005414C5                 add     esp, 2Ch
.text:005414C8                 pop     edi
.text:005414C9                 pop     esi
.text:005414CA                 pop     ebp
.text:005414CB                 pop     ebx
.text:005414CC                 retn
.text:005414CC decrypt_file    endp
\end{lstlisting}

Voici le code C reconstruit:

\begin{lstlisting}[style=customc]
void decrypt_file(char *fin, char* fout, char *pw)
{
	FILE *f;
	int real_flen, flen;
	BYTE *buf;

	f=fopen(fin, "rb");
	
	if (f==NULL)
	{
		printf ("Cannot open input file!\n");
		return;
	};

	fseek (f, 0, SEEK_FRD);
	flen=ftell (f);
	fseek (f, 0, SEEK_SET);

	buf=(BYTE*)malloc (flen);

	fread (buf, flen, 1, f);

	fclose (f);

	if (memcmp (buf, "QR9", 3)!=0)
	{
		printf ("File is not encrypted!\n");
		return;
	};

	memcpy (&real_flen, buf+3, 4);

	decrypt (buf+(3+4), flen-(3+4), pw);
	
	f=fopen(fout, "wb");

	fwrite (buf+(3+4), real_flen, 1, f);

	fclose (f);

	free (buf);
};
\end{lstlisting}

OK, maintenant, approfondissons.

Fonction \TT{crypt()}:

\begin{lstlisting}[style=customasmx86]
.text:00541260 crypt           proc near
.text:00541260
.text:00541260 arg_0           = dword ptr  4
.text:00541260 arg_4           = dword ptr  8
.text:00541260 arg_8           = dword ptr  0Ch
.text:00541260
.text:00541260                 push    ebx
.text:00541261                 mov     ebx, [esp+4+arg_0]
.text:00541265                 push    ebp
.text:00541266                 push    esi
.text:00541267                 push    edi
.text:00541268                 xor     ebp, ebp
.text:0054126A
.text:0054126A loc_54126A:
\end{lstlisting}

\myindex{x86!\Instructions!MOVSD}
Ce morceau de code copie une partie du buffer d'entrée dans un tableau interne que
nous appellerons \q{cube64}.
La taille se trouve dans le registre \ECX. \TT{MOVSD} signifie \emph{déplacer un mot de 32-bit},
donc, 16 mots de 32-bit font exactement 64 octets.

\begin{lstlisting}[style=customasmx86]
.text:0054126A                 mov     eax, [esp+10h+arg_8]
.text:0054126E                 mov     ecx, 10h
.text:00541273                 mov     esi, ebx   ; EBX pointe dans le buffer d'entrée
.text:00541275                 mov     edi, offset cube64
.text:0054127A                 push    1
.text:0054127C                 push    eax
.text:0054127D                 rep movsd
\end{lstlisting}

Appelle \TT{rotate\_all\_with\_password()}:

\begin{lstlisting}[style=customasmx86]
.text:0054127F                 call    rotate_all_with_password
\end{lstlisting}

Copie le contenu chiffré de \q{cube64} dans le buffer:

\begin{lstlisting}[style=customasmx86]
.text:00541284                 mov     eax, [esp+18h+arg_4]
.text:00541288                 mov     edi, ebx
.text:0054128A                 add     ebp, 40h
.text:0054128D                 add     esp, 8
.text:00541290                 mov     ecx, 10h
.text:00541295                 mov     esi, offset cube64
.text:0054129A                 add     ebx, 40h  ; ajouter 64 au pointeur sur le buffer d'entrée
.text:0054129D                 cmp     ebp, eax  ; EBP = amount of encrypted data.
.text:0054129F                 rep movsd
\end{lstlisting}

Si \EBP n'est pas plus grand que la taille de l'argument en entrée, alors continuer
vers le bloc suivant.

\begin{lstlisting}[style=customasmx86]
.text:005412A1                 jl      short loc_54126A
.text:005412A3                 pop     edi
.text:005412A4                 pop     esi
.text:005412A5                 pop     ebp
.text:005412A6                 pop     ebx
.text:005412A7                 retn
.text:005412A7 crypt           endp
\end{lstlisting}

Fonction \TT{crypt()} reconstruite:

\begin{lstlisting}[style=customc]
void crypt (BYTE *buf, int sz, char *pw)
{
	int i=0;
	
	do
	{
		memcpy (cube, buf+i, 8*8);
		rotate_all (pw, 1);
		memcpy (buf+i, cube, 8*8);
		i+=64;
	}
	while (i<sz);
};
\end{lstlisting}

OK, maintenant approfondissons la fonction \TT{rotate\_all\_with\_password()}.
Elle prend deux arguments: la chaîne du mot de passe et un nombre.

Dans \TT{crypt()}, le nombre 1 est utilisé, et dans la fonction \TT{decrypt()} (où
la fonction \TT{rotate\_all\_with\_password()} est aussi appelée), le nombre est 3.

\begin{lstlisting}[style=customasmx86]
.text:005411B0 rotate_all_with_password proc near
.text:005411B0
.text:005411B0 arg_0           = dword ptr  4
.text:005411B0 arg_4           = dword ptr  8
.text:005411B0
.text:005411B0                 mov     eax, [esp+arg_0]
.text:005411B4                 push    ebp
.text:005411B5                 mov     ebp, eax
\end{lstlisting}

Vérifie le caractère courant dans le mot de passe. Si c'est zéro, sort:

\begin{lstlisting}[style=customasmx86]
.text:005411B7                 cmp     byte ptr [eax], 0
.text:005411BA                 jz      exit
.text:005411C0                 push    ebx
.text:005411C1                 mov     ebx, [esp+8+arg_4]
.text:005411C5                 push    esi
.text:005411C6                 push    edi
.text:005411C7
.text:005411C7 loop_begin:
\end{lstlisting}

\myindex{\CStandardLibrary!tolower()}
Appelle \TT{tolower()}, une fonction C standard.

\begin{lstlisting}[style=customasmx86]
.text:005411C7                 movsx   eax, byte ptr [ebp+0]
.text:005411CB                 push    eax             ; C
.text:005411CC                 call    _tolower
.text:005411D1                 add     esp, 4
\end{lstlisting}

Hmm, si le mot de passe comprend des caractères non-Latin, il est sauté!
En effet, lorsqu'on lance l'utilitaire de chiffrement et qu'on utilise des caractères
non-Latin dans le mot de passe, ils semblent être ignorés.

\begin{lstlisting}[style=customasmx86]
.text:005411D4                 cmp     al, 'a'
.text:005411D6                 jl      short next_character_in_password
.text:005411D8                 cmp     al, 'z'
.text:005411DA                 jg      short next_character_in_password
.text:005411DC                 movsx   ecx, al
\end{lstlisting}

Soustrait la valeur de \q{a} (97) du caractère.

\begin{lstlisting}[style=customasmx86]
.text:005411DF                 sub     ecx, 'a'  ; 97
\end{lstlisting}

Après avoir soustrait, nous obtenons 0 pour \q{a} , 1 pour \q{b}, etc. Et 25 pour
\q{z}.

\begin{lstlisting}[style=customasmx86]
.text:005411E2                 cmp     ecx, 24
.text:005411E5                 jle     short skip_subtracting
.text:005411E7                 sub     ecx, 24
\end{lstlisting}

Il semble que \q{y} et \q{z} soient aussi des caractères particuliers.
Après ce morceau de code, \q{y} devient 0 et \q{z}~---1.
Ceci implique que les 26 symboles de l'alphabet Latin deviennent des valeurs dans
l'intervalle 0..23, (24 en tout).

\begin{lstlisting}[style=customasmx86]
.text:005411EA
.text:005411EA skip_subtracting:                       ; CODE XREF: rotate\_all\_with\_password+35
\end{lstlisting}

Ceci est en fait la division par la multiplication.
Vous pouvez lire des précisions dans la section \q{\DivisionByMultSectionName}~(\myref{sec:divisionbymult}).

Le code divise en fait la valeur des caractères du mot de passe par 3.
% TODO1: add Mathematica calculations
\begin{lstlisting}[style=customasmx86]
.text:005411EA                 mov     eax, 55555556h
.text:005411EF                 imul    ecx
.text:005411F1                 mov     eax, edx
.text:005411F3                 shr     eax, 1Fh
.text:005411F6                 add     edx, eax
.text:005411F8                 mov     eax, ecx
.text:005411FA                 mov     esi, edx
.text:005411FC                 mov     ecx, 3
.text:00541201                 cdq
.text:00541202                 idiv    ecx
\end{lstlisting}

\EDX contient le reste de la division.

\lstinputlisting[style=customasmx86]{examples/qr9/4_FR}

Si le reste est 2, appeler \TT{rotate3()}. 
\EDI contient le second argument de la fonction \TT{rotate\_all\_with\_password()}.
Comme nous l'avons déjà noté, 1 est pour l'opération de chiffrement et 3 pour le
déchiffrement.
Donc, il y a une boucle. Lorsque l'on chiffre, rotate1/2/3 sont appelées le même nombre
de fois qu'indiqué par le premier argument.

\begin{lstlisting}[style=customasmx86]
.text:00541215 call_rotate3:
.text:00541215                 push    esi
.text:00541216                 call    rotate3
.text:0054121B                 add     esp, 4
.text:0054121E                 dec     edi
.text:0054121F                 jnz     short call_rotate3
.text:00541221                 jmp     short next_character_in_password
.text:00541223
.text:00541223 call_rotate2:
.text:00541223                 test    ebx, ebx
.text:00541225                 jle     short next_character_in_password
.text:00541227                 mov     edi, ebx
.text:00541229
.text:00541229 loc_541229:
.text:00541229                 push    esi
.text:0054122A                 call    rotate2
.text:0054122F                 add     esp, 4
.text:00541232                 dec     edi
.text:00541233                 jnz     short loc_541229
.text:00541235                 jmp     short next_character_in_password
.text:00541237
.text:00541237 call_rotate1:
.text:00541237                 test    ebx, ebx
.text:00541239                 jle     short next_character_in_password
.text:0054123B                 mov     edi, ebx
.text:0054123D
.text:0054123D loc_54123D:
.text:0054123D                 push    esi
.text:0054123E                 call    rotate1
.text:00541243                 add     esp, 4
.text:00541246                 dec     edi
.text:00541247                 jnz     short loc_54123D
.text:00541249
\end{lstlisting}

Prend le caractère suivant dans la chaîne du mot de passe.

\begin{lstlisting}[style=customasmx86]
.text:00541249 next_character_in_password:
.text:00541249                 mov     al, [ebp+1]
\end{lstlisting}

\gls{increment}{Incrémenter} le pointeur sur le caractère de la chaîne du mot de
passe:

\begin{lstlisting}[style=customasmx86]
.text:0054124C                 inc     ebp
.text:0054124D                 test    al, al
.text:0054124F                 jnz     loop_begin
.text:00541255                 pop     edi
.text:00541256                 pop     esi
.text:00541257                 pop     ebx
.text:00541258
.text:00541258 exit:
.text:00541258                 pop     ebp
.text:00541259                 retn
.text:00541259 rotate_all_with_password endp
\end{lstlisting}

Voici le code C reconstruit:

\begin{lstlisting}[style=customc]
void rotate_all (char *pwd, int v)
{
	char *p=pwd;

	while (*p)
	{
		char c=*p;
		int q;

		c=tolower (c);

		if (c>='a' && c<='z')
		{
			q=c-'a';
			if (q>24)
				q-=24;

			int quotient=q/3;
			int remainder=q % 3;

			switch (remainder)
			{
			case 0: for (int i=0; i<v; i++) rotate1 (quotient); break;
			case 1: for (int i=0; i<v; i++) rotate2 (quotient); break;
			case 2: for (int i=0; i<v; i++) rotate3 (quotient); break;
			};
		};

		p++;
	};
};
\end{lstlisting}

Approfondissons et examinons les fonctions rotate1/2/3.
Chaque fonction appelle deux autres fonctions.
Nous les appellerons \TT{set\_bit()} et \TT{get\_bit()}.

Commençons avec \TT{get\_bit()}:

\begin{lstlisting}[style=customasmx86]
.text:00541050 get_bit         proc near
.text:00541050
.text:00541050 arg_0           = dword ptr  4
.text:00541050 arg_4           = dword ptr  8
.text:00541050 arg_8           = byte ptr  0Ch
.text:00541050
.text:00541050                 mov     eax, [esp+arg_4]
.text:00541054                 mov     ecx, [esp+arg_0]
.text:00541058                 mov     al, cube64[eax+ecx*8]
.text:0054105F                 mov     cl, [esp+arg_8]
.text:00541063                 shr     al, cl
.text:00541065                 and     al, 1
.text:00541067                 retn
.text:00541067 get_bit         endp
\end{lstlisting}

\dots autrement dit: calcule un index dans le tableau cube64: \emph{arg\_4 + arg\_0 * 8}.
Puis décale un octet du tableau de arg\_8 bits à droite.
Isole le bit le plus bas et le renvoie.

Voyons l'autre fonction, \TT{set\_bit()}:

\begin{lstlisting}[style=customasmx86]
.text:00541000 set_bit         proc near
.text:00541000
.text:00541000 arg_0           = dword ptr  4
.text:00541000 arg_4           = dword ptr  8
.text:00541000 arg_8           = dword ptr  0Ch
.text:00541000 arg_C           = byte ptr  10h
.text:00541000
.text:00541000                 mov     al, [esp+arg_C]
.text:00541004                 mov     ecx, [esp+arg_8]
.text:00541008                 push    esi
.text:00541009                 mov     esi, [esp+4+arg_0]
.text:0054100D                 test    al, al
.text:0054100F                 mov     eax, [esp+4+arg_4]
.text:00541013                 mov     dl, 1
.text:00541015                 jz      short loc_54102B
\end{lstlisting}

La valeur dans \TT{DL} est 1 ici. Il est décalé à gauche de arg\_8.
Par exemple, si arg\_8 vaut 4, la valeur dans le registre \TT{DL} doit être 0x10
ou 1000b au format binaire.

\begin{lstlisting}[style=customasmx86]
.text:00541017                 shl     dl, cl
.text:00541019                 mov     cl, cube64[eax+esi*8]
\end{lstlisting}

Prend un bit du tableau et le met explicitement à 1.

\begin{lstlisting}[style=customasmx86]
.text:00541020                 or      cl, dl
\end{lstlisting}

Le sauve à sa place dans le tableau:

\begin{lstlisting}[style=customasmx86]
.text:00541022                 mov     cube64[eax+esi*8], cl
.text:00541029                 pop     esi
.text:0054102A                 retn
.text:0054102B
.text:0054102B loc_54102B:
.text:0054102B                 shl     dl, cl
\end{lstlisting}

Si arg\_C n'est pas zéro\dots

\begin{lstlisting}[style=customasmx86]
.text:0054102D                 mov     cl, cube64[eax+esi*8]
\end{lstlisting}

\myindex{x86!\Instructions!NOT}

\dots inverse DL. Par exemple, si l'état de DL après le décalage est 0x10 ou 0b1000,
Il y aura 0xEF après l'instruction \NOT (ou 0b11101111b).

\begin{lstlisting}[style=customasmx86]
.text:00541034                 not     dl
\end{lstlisting}

Cette instruction efface le bit, autrement dit, elle sauve tous les bits de \TT{CL}
qui sont aussi à 1 dans \TT{DL}, excepté ceux de \TT{DL} qui sont à zéro.
Ceci implique que si \TT{DL} contient 11101111b en format binaire, tous les bits
sont sauvés, à l'exception du 5ème (en comptant depuis le bit le plus bas).

\begin{lstlisting}[style=customasmx86]
.text:00541036                 and     cl, dl
\end{lstlisting}

Le sauve à sa place dans le tableau:

\begin{lstlisting}[style=customasmx86]
.text:00541038                 mov     cube64[eax+esi*8], cl
.text:0054103F                 pop     esi
.text:00541040                 retn
.text:00541040 set_bit         endp
\end{lstlisting}

C'est presque la même chose que \TT{get\_bit()}, excepté que si arg\_C est zéro, la
fonction efface le bit spécifique dans le tableau.

Nous savons aussi que la taille du tableau est 64. Les deux premiers arguments, des
deux fonctions \TT{set\_bit()} et \TT{get\_bit()} peuvent être vus comme des coordonnées
2D. Alors le tableau doit être une matrice 8*8.

Voici une représentation en C de ce que nous savons maintenant:

\begin{lstlisting}[style=customc]
#define IS_SET(flag, bit)       ((flag) & (bit))
#define SET_BIT(var, bit)       ((var) |= (bit))
#define REMOVE_BIT(var, bit)    ((var) &= ~(bit))

static BYTE cube[8][8];

void set_bit (int x, int y, int shift, int bit)
{
	if (bit)
		SET_BIT (cube[x][y], 1<<shift);
	else
		REMOVE_BIT (cube[x][y], 1<<shift);
};

bool get_bit (int x, int y, int shift)
{
	if ((cube[x][y]>>shift)&1==1)
		return 1;
	return 0;
};
\end{lstlisting}

Maintenant, retournons aux fonctions rotate1/2/3.

\begin{lstlisting}[style=customasmx86]
.text:00541070 rotate1         proc near
.text:00541070
\end{lstlisting}

Allocation d'un tableau interne sur la pile locale, avec une taille de 64 octets:

\begin{lstlisting}[style=customasmx86]
.text:00541070 internal_array_64= byte ptr -40h
.text:00541070 arg_0           = dword ptr  4
.text:00541070
.text:00541070                 sub     esp, 40h
.text:00541073                 push    ebx
.text:00541074                 push    ebp
.text:00541075                 mov     ebp, [esp+48h+arg_0]
.text:00541079                 push    esi
.text:0054107A                 push    edi
.text:0054107B                 xor     edi, edi        ; EDI est le compteur de loop1
\end{lstlisting}

\EBX est un pointeur sur la tableau interne:

\begin{lstlisting}[style=customasmx86]
.text:0054107D                 lea     ebx, [esp+50h+internal_array_64]
.text:00541081
\end{lstlisting}

Ici, nous avons deux boucles imbriquées:

\lstinputlisting[style=customasmx86]{examples/qr9/5_FR}

\dots nous voyons que les deux compteurs de boucle sont dans l'intervalle 0..7.
Ils sont aussi utilisés comme premier et second argument pour la fonction \TT{get\_bit()}.
Le troisième argument de \TT{get\_bit()} est le seul argument de \TT{rotate1()}.
La valeur de retour de \TT{get\_bit()} est mise dans le tableau interne.

Prépare à nouveau un pointeur sur le tableau interne:

\lstinputlisting[style=customasmx86]{examples/qr9/6_FR}

\dots ce code place le contenu du tableau interne dans le tableau global cube via
la fonction \TT{set\_bit()}.
Maintenant le compteur de la première boucle est dans l'intervalle 7 à 0, \glslink{decrement}{décrémenté}
à chaque itération!

La représentation en C ressemble à:

\begin{lstlisting}[style=customc]
void rotate1 (int v)
{
	bool tmp[8][8]; // internal array
	int i, j;

	for (i=0; i<8; i++)
		for (j=0; j<8; j++)
			tmp[i][j]=get_bit (i, j, v);

	for (i=0; i<8; i++)
		for (j=0; j<8; j++)
			set_bit (j, 7-i, v, tmp[x][y]);
};
\end{lstlisting}

Pas très compréhensible, mais si nous regardons la fonction \TT{rotate2()}:

\lstinputlisting[style=customasmx86]{examples/qr9/7_FR}

C'est presque la même, à part l'ordre des arguments à \TT{get\_bit()} et \TT{set\_bit()}
qui est différent.
Récrivons-là en pseudo-code C:

\begin{lstlisting}[style=customc]
void rotate2 (int v)
{
	bool tmp[8][8]; // internal array
	int i, j;

	for (i=0; i<8; i++)
		for (j=0; j<8; j++)
			tmp[i][j]=get_bit (v, i, j);

	for (i=0; i<8; i++)
		for (j=0; j<8; j++)
			set_bit (v, j, 7-i, tmp[i][j]);
};
\end{lstlisting}

Récrivons aussi la fonction \TT{rotate3()}:

\begin{lstlisting}[style=customc]
void rotate3 (int v)
{
	bool tmp[8][8];
	int i, j;

	for (i=0; i<8; i++)
		for (j=0; j<8; j++)
			tmp[i][j]=get_bit (i, v, j);

	for (i=0; i<8; i++)
		for (j=0; j<8; j++)
			set_bit (7-j, v, i, tmp[i][j]);
};
\end{lstlisting}

Bon, les choses sont plus simples maintenant. Si nous considérons cube64 comme un
cube de dimension 8*8*8, où chaque élément est un bit, \TT{get\_bit()} et \TT{set\_bit()}
prennent simplement les coordonnées d'un bit en entrée.

Les fonctions rotate1/2/3 tournent en fait tous les bits dans un plan spécifique.
Ces trois fonctions sont une pour chaque axe du cube et l'argument \TT{v} défini
le plan dans l'intervalle 0..7.

Peut-être que l'auteur de l'algorithme pensait à un Rubik's cube 8*8*8
\footnote{\href{http://en.wikipedia.org/wiki/Rubik's_Cube}{Wikipédia}}?!

Oui, en effet.

Regardons de plus près la fonction \TT{decrypt()}, voici la version récrite:

\begin{lstlisting}[style=customc]
void decrypt (BYTE *buf, int sz, char *pw)
{
	char *p=strdup (pw);
	strrev (p);
	int i=0;

	do
	{
		memcpy (cube, buf+i, 8*8);
		rotate_all (p, 3);
		memcpy (buf+i, cube, 8*8);
		i+=64;
	}
	while (i<sz);
	
	free (p);
};
\end{lstlisting}

C'est presque la même que pour \TT{crypt()}, \emph{mais} la chaîne du mot de passe
est inversée par la fonction C standard strrev() \footnote{\href{http://msdn.microsoft.com/en-us/library/9hby7w40(VS.80).aspx}{MSDN}}
et \TT{rotate\_all()} est appelé avec l'argument 3.

Ceci implique que dans le cas du déchiffrement, chaque appel correspondant à rotate1/2/3
est effectué trois fois.

C'est presque la même chose qu'avec le Rubik's cube!
Si vous voulez aller en arrière, faites la même chose dans l'ordre et direction inverse!
Si vous voulez annuler l'effet de tourner une position dans le sens des aiguilles
d'une montre, tournez une fois dans le sens inverse, ou trois fois dans le même sens.

\TT{rotate1()} semble tourner le plan de face.
\TT{rotate2()} semble tourner le plan du dessus.
\TT{rotate3()} semble tourner le plan de gauche.

Retournons au c\oe{}ur de la fonction \TT{rotate\_all()}:

\begin{lstlisting}[style=customc]
q=c-'a';
if (q>24)
	q-=24;

int quotient=q/3; // in range 0..7
int remainder=q % 3;

switch (remainder)
{
    case 0: for (int i=0; i<v; i++) rotate1 (quotient); break; // front
    case 1: for (int i=0; i<v; i++) rotate2 (quotient); break; // top
    case 2: for (int i=0; i<v; i++) rotate3 (quotient); break; // left
};
\end{lstlisting}

Maintenant, c'est bien plus facile à comprendre: chaque caractère du mot de passe
définit un axe (un des trois) et un plan (un des 8).
3*8 = 24, c'est pourquoi les deux derniers caractères de l'alphabet Latin sont remappés
pour obtenir un alphabet d'exactement 24 éléments.

Cet algorithme est clairement faible: dans le cas de mots passe courts, vous pouvez
voir dans le fichier chiffré des octets du fichier original dans un éditeur binaire.

Voici le code source reconstruit complet:

\lstinputlisting[style=customc]{examples/qr9/qr9.cpp}

