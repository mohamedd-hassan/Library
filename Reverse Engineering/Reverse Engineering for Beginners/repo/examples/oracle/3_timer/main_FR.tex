\subsection{Table \TT{V\$TIMER} dans \oracle}
\myindex{\oracle}

\TT{V\$TIMER} 
est une autre \emph{vue fixée} qui reflète une valeur changeant rapidement:

\begin{framed}
\begin{quotation}
V\$TIMER affiche le temps écoulé en centièmes de seconde. Le temps est mesuré depuis
le début de the epoch, qui est dépendant du système d'exploitation, et qui redevient
0 si la valeur déborde quatre octets (environ 497 jours).
\end{quotation}
\end{framed}(From \oracle documentation
\footnote{\url{http://docs.oracle.com/cd/B28359_01/server.111/b28320/dynviews_3104.htm}})


Il est intéressant que les périodes soient différentes pour Oracle pour win32 et
pour Linux.
Allons-nous réussir à trouver la fonction qui génère cette valeur?

On voit que cette valeur est finalement prise de la table \TT{X\$KSUTM}.

\begin{lstlisting}
SQL> select * from V$FIXED_VIEW_DEFINITION where view_name='V$TIMER';

VIEW_NAME
------------------------------
VIEW_DEFINITION
------------------------------

V$TIMER
select  HSECS from GV$TIMER where inst_id = USERENV('Instance')

SQL> select * from V$FIXED_VIEW_DEFINITION where view_name='GV$TIMER';

VIEW_NAME
------------------------------
VIEW_DEFINITION
------------------------------

GV$TIMER
select inst_id,ksutmtim from x$ksutm
\end{lstlisting}

Nous maintenant bloqué par un petit problème, il n'y a pas de référence à une ou des
fonction(s) générants des valeurs dans les tables \TT{kqftab}/\TT{kqftap}:

\begin{lstlisting}[caption=Résultat de \OracleTablesName]
kqftab_element.name: [X$KSUTM] ?: [ksutm] 0x1 0x4 0x4 0x0 0xffffc09b 0x3
kqftap_param.name=[ADDR] ?: 0x10917 0x0 0x0 0x0 0x4 0x0 0x0
kqftap_param.name=[INDX] ?: 0x20b02 0x0 0x0 0x0 0x4 0x0 0x0
kqftap_param.name=[INST_ID] ?: 0xb02 0x0 0x0 0x0 0x4 0x0 0x0
kqftap_param.name=[KSUTMTIM] ?: 0x1302 0x0 0x0 0x0 0x4 0x0 0x1e
kqftap_element.fn1=NULL
kqftap_element.fn2=NULL
\end{lstlisting}

Lorsque nous essayons de trouver la chaîne \TT{KSUTMTIM}, nous la voyons dans cette
fonction:

\begin{lstlisting}[style=customasmx86]
kqfd_DRN_ksutm_c proc near    ; DATA XREF: .rodata:0805B4E8

arg_0   = dword ptr  8
arg_8   = dword ptr  10h
arg_C   = dword ptr  14h

        push    ebp
        mov     ebp, esp
        push    [ebp+arg_C]
        push    offset ksugtm
        push    offset _2__STRING_1263_0 ; "KSUTMTIM"
        push    [ebp+arg_8]
        push    [ebp+arg_0]
        call    kqfd_cfui_drain
        add     esp, 14h
        mov     esp, ebp
        pop     ebp
        retn
kqfd_DRN_ksutm_c endp
\end{lstlisting}

La fonction \TT{kqfd\_DRN\_ksutm\_c()} est mentionnée dans la \\
table \TT{kqfd\_tab\_registry\_0}:

\begin{lstlisting}[style=customasmx86]
dd offset _2__STRING_62_0 ; "X\$KSUTM"
dd offset kqfd_OPN_ksutm_c
dd offset kqfd_tabl_fetch
dd 0
dd 0
dd offset kqfd_DRN_ksutm_c
\end{lstlisting}

Il y a une fonction \TT{ksugtm()} référencée ici.
Voyons ce qu'elle contient dans (Linux x86):

\begin{lstlisting}[caption=ksu.o,style=customasmx86]
ksugtm  proc near

var_1C  = byte ptr -1Ch
arg_4   = dword ptr  0Ch

        push    ebp
        mov     ebp, esp
        sub     esp, 1Ch
        lea     eax, [ebp+var_1C]
        push    eax
        call    slgcs
        pop     ecx
        mov     edx, [ebp+arg_4]
        mov     [edx], eax
        mov     eax, 4
        mov     esp, ebp
        pop     ebp
        retn
ksugtm  endp
\end{lstlisting}

Le code dans la version win32 est presque le même.

Est-ce la fonction que nous cherchons? Regardons:
\myindex{tracer}

\begin{lstlisting}
tracer -a:oracle.exe bpf=oracle.exe!_ksugtm,args:2,dump_args:0x4
\end{lstlisting}

Essayons encore:

\begin{lstlisting}
SQL> select * from V$TIMER;

     HSECS
----------
  27294929

SQL> select * from V$TIMER;

     HSECS
----------
  27295006

SQL> select * from V$TIMER;

     HSECS
----------
  27295167
\end{lstlisting}

\begin{lstlisting}[caption=Sortie de \tracer]
TID=2428|(0) oracle.exe!_ksugtm (0x0, 0xd76c5f0) (called from oracle.exe!__VInfreq__qerfxFetch+0xfad (0x56bb6d5))
Argument 2/2
0D76C5F0: 38 C9                                           "8.              "
TID=2428|(0) oracle.exe!_ksugtm () -> 0x4 (0x4)
Argument 2/2 difference
00000000: D1 7C A0 01                                     ".|..            "
TID=2428|(0) oracle.exe!_ksugtm (0x0, 0xd76c5f0) (called from oracle.exe!__VInfreq__qerfxFetch+0xfad (0x56bb6d5))
Argument 2/2
0D76C5F0: 38 C9                                           "8.              "
TID=2428|(0) oracle.exe!_ksugtm () -> 0x4 (0x4)
Argument 2/2 difference
00000000: 1E 7D A0 01                                     ".}..            "
TID=2428|(0) oracle.exe!_ksugtm (0x0, 0xd76c5f0) (called from oracle.exe!__VInfreq__qerfxFetch+0xfad (0x56bb6d5))
Argument 2/2
0D76C5F0: 38 C9                                           "8.              "
TID=2428|(0) oracle.exe!_ksugtm () -> 0x4 (0x4)
Argument 2/2 difference
00000000: BF 7D A0 01                                     ".}..            "
\end{lstlisting}

En effet---la valeur est la même que celle que nous voyons dans SQL*Plus et elle
est renvoyée dans le second argument.

Regardons ce que \TT{slgcs()} contient (Linux x86):

\begin{lstlisting}[style=customasmx86]
slgcs   proc near

var_4   = dword ptr -4
arg_0   = dword ptr  8

        push    ebp
        mov     ebp, esp
        push    esi
        mov     [ebp+var_4], ebx
        mov     eax, [ebp+arg_0]
        call    $+5
        pop     ebx
        nop                     ; PIC mode
        mov     ebx, offset _GLOBAL_OFFSET_TABLE_
        mov     dword ptr [eax], 0
        call    sltrgatime64    ; PIC mode
        push    0
        push    0Ah
        push    edx
        push    eax
        call    __udivdi3       ; PIC mode
        mov     ebx, [ebp+var_4]
        add     esp, 10h
        mov     esp, ebp
        pop     ebp
        retn
slgcs   endp
\end{lstlisting}

(c'est simplement un appel à \TT{sltrgatime64()}

et la division de son résultat par 10 (\myref{sec:divisionbymult}))

Et la version win32:

\begin{lstlisting}[style=customasmx86]
_slgcs  proc near     ; CODE XREF: \_dbgefgHtElResetCount+15
                      ; \_dbgerRunActions+1528
        db      66h
        nop
        push    ebp
        mov     ebp, esp
        mov     eax, [ebp+8]
        mov     dword ptr [eax], 0
        call    ds:__imp__GetTickCount@0 ; GetTickCount()
        mov     edx, eax
        mov     eax, 0CCCCCCCDh
        mul     edx
        shr     edx, 3
        mov     eax, edx
        mov     esp, ebp
        pop     ebp
        retn
_slgcs  endp
\end{lstlisting}

Il s'agit simplement du résultat de \TT{GetTickCount()}
\footnote{\href{http://msdn.microsoft.com/en-us/library/windows/desktop/ms724408(v=vs.85).aspx}{MSDN}}
divisé par 10 (\myref{sec:divisionbymult}).

% TODO add calc

Voilà! C'est pourquoi la version win32 et Linux x86 montrent des résultats différents,
car ils sont générés par des fonctions de l'\ac{OS} différentes.

\emph{Drain} implique apparemment de \emph{connecter} une colonne de table spécifique
à une fonction spécifique.

Nous allons ajouter le support de la table \TT{kqfd\_tab\_registry\_0} à \oracletables,
maintenant nous voyons comment les variables des colonnes de table sont \emph{connectée}
à une fonction spécifique:

\begin{lstlisting}
[X$KSUTM] [kqfd_OPN_ksutm_c] [kqfd_tabl_fetch] [NULL] [NULL] [kqfd_DRN_ksutm_c]
[X$KSUSGIF] [kqfd_OPN_ksusg_c] [kqfd_tabl_fetch] [NULL] [NULL] [kqfd_DRN_ksusg_c]
\end{lstlisting}

\emph{OPN}, signifie apparemment \emph{open}, et \emph{DRN}, apparemment \emph{drain}.

