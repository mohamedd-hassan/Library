\subsection{32ビット定数をレジスタにロードする}
\label{MIPS_big_constants}

\begin{lstlisting}[style=customc]
unsigned int f()
{
	return 0x12345678;
};
\end{lstlisting}

MIPSのすべての命令は、ARMと同様に32ビットのサイズを持っているため、
1つの命令に32ビットの定数を埋め込むことはできません。
\myindex{MIPS!\Instructions!LI}
\myindex{MIPS!\Instructions!ORI}

そのため、少なくとも2つの命令を使用する必要があります。
1つ目は32ビット数の上位部分をロードし、2つ目はターゲットレジスタの
下位16ビット部分を効果的に設定するOR演算を適用します。

\begin{lstlisting}[caption=GCC 4.4.5 -O3 (\assemblyOutput),style=customasmMIPS]
        li      $2,305397760  # 0x12340000
        j       $31
        ori     $2,$2,0x5678 ; 分岐遅延スロット
\end{lstlisting}

\IDA はこのような頻繁に発生するコードパターンを完全に認識しているので、
便宜上、最後の\INS{ORI}命令を\INS{LI}疑似命令として示しています。
それは完全な32ビット数を \$V0 レジスタにロードするとされています。

\myindex{MIPS!\Instructions!LUI}

\begin{lstlisting}[caption=GCC 4.4.5 -O3 (IDA),style=customasmMIPS]
         lui     $v0, 0x1234
         jr      $ra
         li      $v0, 0x12345678 ; 分岐遅延スロット
\end{lstlisting}

GCCアセンブリの出力にはLI疑似命令がありますが、実際には\INS{LUI} (\q{Load Upper Immediate}) であり、
これが16ビット値をレジスタの上位部分に格納します。

\emph{objdump}の出力を見てみましょう。

\begin{lstlisting}[caption=objdump,style=customasmMIPS]
00000000 <f>:
   0:   3c021234        lui     v0,0x1234
   4:   03e00008        jr      ra
   8:   34425678        ori     v0,v0,0x5678
\end{lstlisting}

\subsubsection{32ビットグローバル変数をレジスタにロードする}

\begin{lstlisting}[style=customc]
unsigned int global_var=0x12345678;

unsigned int f2()
{
        return global_var;
};
\end{lstlisting}

\myindex{MIPS!\Instructions!LW}

これは少し異なります。\INS{LUI}は\emph{global\_var}から上位16ビットを \$2 （または \$V0 ）にロードし、次に下位16ビットをロードして \$2 の内容を合計します。

\begin{lstlisting}[caption=GCC 4.4.5 -O3 (\assemblyOutput),style=customasmMIPS]
f2:
        lui     $2,%hi(global_var)
        lw      $2,%lo(global_var)($2)
        j       $31
        nop	; 分岐遅延スロット

	...

global_var:
        .word   305419896
\end{lstlisting}

\IDA はよく使用される\INS{LUI}/\INS{LW}命令ペアを完全に認識しているため、両方を1つの\INS{LW}命令に統合します。

\begin{lstlisting}[caption=GCC 4.4.5 -O3 (IDA),style=customasmMIPS]
_f2:
                lw      $v0, global_var
                jr      $ra
                or      $at, $zero	; 分岐遅延スロット

		...

                .data
                .globl global_var
global_var:     .word 0x12345678         # DATA XREF: \_f2
\end{lstlisting}

\emph{objdump}の出力はGCCのアセンブリ出力と同じです。 
オブジェクトファイルの再配置もダンプしましょう。

\begin{lstlisting}[caption=objdump,style=customasmMIPS]
objdump -D filename.o

...

0000000c <f2>:
   c:   3c020000        lui     v0,0x0
  10:   8c420000        lw      v0,0(v0)
  14:   03e00008        jr      ra
  18:   00200825        move    at,at	; 分岐遅延スロット
  1c:   00200825        move    at,at

Disassembly of section .data:

00000000 <global_var>:
   0:   12345678        beq     s1,s4,159e4 <f2+0x159d8>

...

objdump -r filename.o

...

RELOCATION RECORDS FOR [.text]:
OFFSET   TYPE              VALUE
0000000c R_MIPS_HI16       global_var
00000010 R_MIPS_LO16       global_var

...

\end{lstlisting}

\emph{global\_var}のアドレスは、実行可能ファイルのロード中に\INS{LUI}および\INS{LW}命令に直接書き込まれることがわかります。
\emph{global\_var}の上位16ビット部分は最初のもの（\INS{LUI}）に、下位16ビット部分は2番目のもの（\INS{LW}）に入ります。
