% TODO proof-reading
\subsection{Exceptions}

Retravaillons un peu notre exemple \emph{Month} (\myref{Java_2D_array_month}):

\begin{lstlisting}[caption=IncorrectMonthException.java,style=customjava]
public class IncorrectMonthException extends Exception
{
	private int index;

	public IncorrectMonthException(int index)
	{
		this.index = index;
	}
	public int getIndex()
	{
		return index;
	}
}
\end{lstlisting}

\begin{lstlisting}[caption=Month2.java,style=customjava]
class Month2
{
	public static String[] months =
	{
		"January",
		"February",
		"March",
		"April",
		"May",
		"June",
		"July",
		"August",
		"September",
		"October",
		"November",
		"December"
	};

	public static String get_month (int i) throws IncorrectMonthException
	{
		if (i<0 || i>11)
			throw new IncorrectMonthException(i);
		return months[i];
	};

	public static void main (String[] args)
	{
		try
		{
			System.out.println(get_month(100));
		}
		catch(IncorrectMonthException e)
		{
			System.out.println("incorrect month index: "+ e.getIndex());
			e.printStackTrace();
		}
	};
}
\end{lstlisting}

En gros,  \TT{IncorrectMonthException.class} possède juste un objet constructeur
et une méthode getter.

La classe \TT{IncorrectMonthException} est dérivée d'\TT{Exception}, donc le constructeur
de\\
\TT{IncorrectMonthException} appelle d'abord le constructeur de la classe \TT{Exception},
puis il met la valeur entière en entrée dans l'unique champ de la classe \TT{IncorrectMonthException}:

\begin{lstlisting}
  public IncorrectMonthException(int);
    flags: ACC_PUBLIC
    Code:
      stack=2, locals=2, args_size=2
         0: aload_0
         1: invokespecial #1        // Method java/lang/Exception."<init>":()V
         4: aload_0
         5: iload_1
         6: putfield      #2        // Field index:I
         9: return
\end{lstlisting}

\TT{getIndex()} est simplement un getter.
Une \emph{référence} sur \TT{IncorrectMonthException} est passée dans le slot zéro
du \ac{LVA} (\emph{this}), \TT{aload\_0} le prend, \TT{getfield} charge une valeur
entière depuis l'objet, \TT{ireturn} la renvoie.

\begin{lstlisting}
  public int getIndex();
    flags: ACC_PUBLIC
    Code:
      stack=1, locals=1, args_size=1
         0: aload_0
         1: getfield      #2        // Field index:I
         4: ireturn
\end{lstlisting}

Maintenant, regardons \TT{get\_month()} dans \TT{Month2.class}:

\begin{lstlisting}[caption=Month2.class]
  public static java.lang.String get_month(int) throws IncorrectMonthException;
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=3, locals=1, args_size=1
         0: iload_0
         1: iflt          10
         4: iload_0
         5: bipush        11
         7: if_icmple     19
        10: new           #2        // class IncorrectMonthException
        13: dup
        14: iload_0
        15: invokespecial #3        // Method IncorrectMonthException."<init>":(I)V
        18: athrow
        19: getstatic     #4        // Field months:[Ljava/lang/String;
        22: iload_0
        23: aaload
        24: areturn
\end{lstlisting}

\TT{iflt} à l'offset 1 est \emph{if less than}.

Dans le cas d'un index invalide, un nouvel objet est créé en utilisant l'instruction
\TT{new} à l'offset 10.

Le type de l'objet est passé comme un opérande à l'instruction (qui est \TT{IncorrectMonthException}).

Ensuite, son constructeur est appelé et l'index est passé via le \ac{TOS} (offset 15).

Lorsque le contrôle du flux se trouve à l'offset 18, l'objet est déjà construit,
donc maintenant l'instruction \TT{athrow} prend une \emph{référence} sur l'objet
nouvellement construit et indique à la \ac{JVM} de trouver le gestionnaire d'exception
approprié.

L'instruction \TT{athrow} ne renvoie pas le contrôle du flus ici, donc à l'offset
19 il y a un autre \glslink{basic block}{bloc de base}, non relatif aux exceptions,
où nous pouvons aller depuis l'offset 7.

Comment fonctionnent les gestionnaires?

\main in \TT{Month2.class}:

\begin{lstlisting}[caption=Month2.class]
  public static void main(java.lang.String[]);
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=3, locals=2, args_size=1
         0: getstatic     #5        // Field java/lang/System.out:Ljava/io/PrintStream;
         3: bipush        100
         5: invokestatic  #6        // Method get_month:(I)Ljava/lang/String;
         8: invokevirtual #7        // Method java/io/PrintStream.println:(Ljava/lang/String;)V
        11: goto          47
        14: astore_1
        15: getstatic     #5        // Field java/lang/System.out:Ljava/io/PrintStream;
        18: new           #8        // class java/lang/StringBuilder
        21: dup
        22: invokespecial #9        // Method java/lang/StringBuilder."<init>":()V
        25: ldc           #10       // String incorrect month index:
        27: invokevirtual #11       // Method java/lang/StringBuilder.append:(Ljava/lang/String;)Ljava/lang/StringBuilder;
        30: aload_1
        31: invokevirtual #12       // Method IncorrectMonthException.getIndex:()I
        34: invokevirtual #13       // Method java/lang/StringBuilder.append:(I)Ljava/lang/StringBuilder;
        37: invokevirtual #14       // Method java/lang/StringBuilder.toString:()Ljava/lang/String;
        40: invokevirtual #7        // Method java/io/PrintStream.println:(Ljava/lang/String;)V
        43: aload_1
        44: invokevirtual #15       // Method IncorrectMonthException.printStackTrace:()V
        47: return
      Exception table:
         from    to  target type
             0    11    14   Class IncorrectMonthException
\end{lstlisting}

Ici se trouve la \TT{table Exception}, qui définit que de l'offset 0 à 11 (inclus), une exception \\
\TT{IncorrectMonthException} peut se produire, et si cela se produit, le contrôle
du flux sera passé à l'offset 14.

En effet, le programme principal se termine à l'offset 11.

À l'offset 14, le gestionnaire commence. Il n'est pas possible d'arriver ici,
il n'y a pas de saut conditionnel/inconditionnel à cet endroit.

Mais la \ac{JVM} transférera le flux d'exécution ici en cas d'exception.

Le tout premier \TT{astore\_1} (en 14) prend la \emph{référence} en entrée  sur l'objet
exception et la stocke dans le slot 1 du \ac{LVA}.

Plus tard, la méthode \TT{getIndex()} (de cet objet exception) sera appelée à l'offset 31.

La \emph{référence} sur l'objet exception courant est passée juste avant cela (offset 30).

Le reste du code effectue juste de la manipulation de chaîne:
d'abord. la valeur entière renvoyée par \TT{getIndex()} est convertie en chaîne par
la méthode \TT{toString()}, puis est concaténée avec la chaîne de texte \q{incorrect month index: }
(comme nous l'avons vu avant), enfin \TT{println()} et \TT{printStackTrace()} sont
appelées.

Après la fin de \TT{printStackTrace()}, l'exception est gérer et nous pouvons continuer
avec l'exécution normale.

À l'offset 47 il y a un \TT{return} qui termine la fonction \main, mais il pourrait
y avoir n'importe quel autre code qui serait exécuté comme si aucune exception n'avait
été déclenchée.

Voici un exemple de la façon dont \IDA montre les intervalles d'exceptions:


\begin{lstlisting}[caption=tiré d'un fichier .class quelconque trouvé sur mon ordinateur]
    .catch java/io/FileNotFoundException from met001_335 to met001_360\
 using met001_360
    .catch java/io/FileNotFoundException from met001_185 to met001_214\
 using met001_214
    .catch java/io/FileNotFoundException from met001_181 to met001_192\
 using met001_195
    .catch java/io/FileNotFoundException from met001_155 to met001_176\
 using met001_176
    .catch java/io/FileNotFoundException from met001_83 to met001_129 using \
met001_129
    .catch java/io/FileNotFoundException from met001_42 to met001_66 using \
met001_69
    .catch java/io/FileNotFoundException from met001_begin to met001_37\
 using met001_37
\end{lstlisting}
