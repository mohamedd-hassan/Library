% TODO proof-reading
\subsection{Conditional jumps}

Maintenant, continuons avec les sauts conditionnels.

\begin{lstlisting}[style=customjava]
public class abs
{
	public static int abs(int a)
	{
		if (a<0)
			return -a;
		return a;
	}
}
\end{lstlisting}

\begin{lstlisting}
  public static int abs(int);
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=1, locals=1, args_size=1
         0: iload_0
         1: ifge          7
         4: iload_0
         5: ineg
         6: ireturn
         7: iload_0
         8: ireturn
\end{lstlisting}

\TT{ifge} saute à l'offset 7 si la valeur du \ac{TOS} est plus grande ou égale à 0.

N'oubliez pas que chaque instruction \TT{ifXX} supprime la valeur (qui doit être
comparée) de la pile.


\TT{ineg} inverse le signe de la valeur du \ac{TOS}.


Un autre exemple:

\begin{lstlisting}[style=customjava]
	public static int min (int a, int b)
	{
		if (a>b)
			return b;
		return a;
	}
\end{lstlisting}

Nous obtenons:

\begin{lstlisting}
  public static int min(int, int);
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=2, locals=2, args_size=2
         0: iload_0
         1: iload_1
         2: if_icmple     7
         5: iload_1
         6: ireturn
         7: iload_0
         8: ireturn
\end{lstlisting}

\TT{if\_icmple} prend deux valeurs et les compare.
Si la seconde est plus petite ou égale à la première, un saut à l'offset 7 est effectué.


Quand nous définissons la fonction \TT{max()} \dots


\begin{lstlisting}[style=customjava]
	public static int max (int a, int b)
	{
		if (a>b)
			return a;
		return b;
	}
\end{lstlisting}

\dots le code résultant est le même, mais les deux dernières instructions \TT{iload}
(aux offsets 5 et 7) sont échangées:

\begin{lstlisting}
  public static int max(int, int);
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=2, locals=2, args_size=2
         0: iload_0
         1: iload_1
         2: if_icmple     7
         5: iload_0
         6: ireturn
         7: iload_1
         8: ireturn
\end{lstlisting}

Un exemple plus avancé:

\begin{lstlisting}[style=customjava]
public class cond
{
	public static void f(int i)
	{
		if (i<100)
			System.out.print("<100");
		if (i==100)
			System.out.print("==100");
		if (i>100)
			System.out.print(">100");
		if (i==0)
			System.out.print("==0");
	}
}
\end{lstlisting}

\begin{lstlisting}
  public static void f(int);
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=2, locals=1, args_size=1
         0: iload_0
         1: bipush        100
         3: if_icmpge     14
         6: getstatic     #2        // Field java/lang/System.out:Ljava/io/PrintStream;
         9: ldc           #3        // String <100
        11: invokevirtual #4        // Method java/io/PrintStream.print:(Ljava/lang/String;)V
        14: iload_0
        15: bipush        100
        17: if_icmpne     28
        20: getstatic     #2        // Field java/lang/System.out:Ljava/io/PrintStream;
        23: ldc           #5        // String ==100
        25: invokevirtual #4        // Method java/io/PrintStream.print:(Ljava/lang/String;)V
        28: iload_0
        29: bipush        100
        31: if_icmple     42
        34: getstatic     #2        // Field java/lang/System.out:Ljava/io/PrintStream;
        37: ldc           #6        // String >100
        39: invokevirtual #4        // Method java/io/PrintStream.print:(Ljava/lang/String;)V
        42: iload_0
        43: ifne          54
        46: getstatic     #2        // Field java/lang/System.out:Ljava/io/PrintStream;
        49: ldc           #7        // String ==0
        51: invokevirtual #4        // Method java/io/PrintStream.print:(Ljava/lang/String;)V
        54: return
\end{lstlisting}

\TT{if\_icmpge} prend deux valeurs et les compare.
Si le seconde est plus grande que la première, un saut à l'offset 14 est effectué.

\TT{if\_icmpne} et \TT{if\_icmple} fonctionnent de la même façon, mais implémentent
des conditions différentes.


Il y a aussi une instruction \TT{ifne} à l'offset 43.

Le nom est un terme inapproprié, il aurait été meilleur de l'appeler \TT{ifnz} (saut
si la valeur du \ac{TOS} n'est pas zéro).

Et c'est ce qu'elle fait: elle saute à l'offset 54 si la valeur en entrée n'est pas
zéro.

Si c'est zéro, le flux d'exécution continue à l'offset 46, où la chaîne \q{==0} est
affichée.


N.B.: la \ac{JVM} n'a pas de type de données non signée, donc les instructions de
comparaison opèrent seulement sur des valeurs entières signées.

