% TODO proof-reading
\subsection{Appel de fonction simple}

\TT{Math.random()} renvoie un nombre pseudo-aléatoire dans l'intervalle [0.0 \dots 1.0],
mais disons que une certaine raison, nous devons concevoir une fonction qui renvoie
un nombre dans l'intervalle [0.0 \dots 0.5]:


\begin{lstlisting}[style=customjava]
public class HalfRandom
{ 
	public static double f()
	{
		return Math.random()/2;
	}
}
\end{lstlisting}

\begin{lstlisting}[caption=Constant pool]
...
   #2 = Methodref          #18.#19    //  java/lang/Math.random:()D
   #3 = Double             2.0d
...
  #12 = Utf8               ()D
...
  #18 = Class              #22        //  java/lang/Math
  #19 = NameAndType        #23:#12    //  random:()D
  #22 = Utf8               java/lang/Math
  #23 = Utf8               random
\end{lstlisting}

\begin{lstlisting}
  public static double f();
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=4, locals=0, args_size=0
         0: invokestatic  #2          // Method java/lang/Math.random:()D
         3: ldc2_w        #3          // double 2.0d
         6: ddiv
         7: dreturn
\end{lstlisting}

\TT{invokestatic} appelle la fonction \TT{Math.random()} et laisse le résultat sur
le \ac{TOS}.

Le résultat est divisé par 2.0 et renvoyé.

Mais comment est encodé le nom de la fonction?

Il est encodé dans le pool constant en utilisant une expression \TT{Methodref}.

Il définit les noms de classe et méthode.

Le premier champ de \TT{Methodref} pointe sur une expression \TT{Class} qui, à son
tour, pointe sur la chaîne de texte usuel (\q{java/lang/Math}).

La seconde expression de \TT{Methodref} pointe sur une expression \TT{NameAndType}
qui a aussi deux liens sur des chaînes.

La première chaîne est \q{random}, qui est le nom de la méthode.

La seconde chaîne est \q{()D}, qui encode le type de la fonction.
Cela signifie qu'elle renvoie une valeur \emph{double} (d'où le \emph{D} dans la chaîne).

Ceci est la façon dont
1) la JVM peut vérifier la justesse des types de données;
2) les décompilateurs Java peuvent retrouver les types de données depuis un fichier
de classe compilée.


Maintenant, essayons l'exemple \q{Hello, world!}:


\begin{lstlisting}[style=customjava]
public class HelloWorld
{
	public static void main(String[] args)
	{
		System.out.println("Hello, World");
	}
}
\end{lstlisting}

\begin{lstlisting}[caption=Constant pool]
...
   #2 = Fieldref           #16.#17        //  java/lang/System.out:Ljava/io/PrintStream;
   #3 = String             #18            //  Hello, World
   #4 = Methodref          #19.#20        //  java/io/PrintStream.println:(Ljava/lang/String;)V
...
  #16 = Class              #23            //  java/lang/System
  #17 = NameAndType        #24:#25        //  out:Ljava/io/PrintStream;
  #18 = Utf8               Hello, World
  #19 = Class              #26            //  java/io/PrintStream
  #20 = NameAndType        #27:#28        //  println:(Ljava/lang/String;)V
...
  #23 = Utf8               java/lang/System
  #24 = Utf8               out
  #25 = Utf8               Ljava/io/PrintStream;
  #26 = Utf8               java/io/PrintStream
  #27 = Utf8               println
  #28 = Utf8               (Ljava/lang/String;)V
...
\end{lstlisting}

\begin{lstlisting}
  public static void main(java.lang.String[]);
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=2, locals=1, args_size=1
         0: getstatic     #2        // Field java/lang/System.out:Ljava/io/PrintStream;
         3: ldc           #3        // String Hello, World
         5: invokevirtual #4        // Method java/io/PrintStream.println:(Ljava/lang/String;)V
         8: return
\end{lstlisting}

\TT{ldc} à l'offset 3 prend un pointeur sur la chaîne \q{Hello, World} dans le pool
constant et le pousse sur la pile.

C'est appelé une \emph{référence} dans le monde Java, mais c'est plutôt un pointeur,
ou une adresse%
\footnote{À propos de la différence entre pointeurs et \emph{références} en C++ voir: \myref{cpp_references}.}.


L'instruction connue \TT{invokevirtual} prend les informations concernant la fonction
\TT{println} (ou méthode) depuis le pool constant et l'appelle.

Comme on peut le savoir, il y a plusieurs méthodes \TT{println}, une pour chaque
type de données.

Dans notre cas, c'est la version de \TT{println} destinée au type de données \emph{String}.


Mais qu'en est-il de la première instruction \TT{getstatic}?

Cette instruction prend une \emph{référence} (ou l'adresse de) un champ de l'objet
\TT{System.out} et le pousse sur la pile.

Cette valeur se comporte comme le pointeur \emph{this} pour la méthode \TT{println}.

Ainsi, en interne, la méthode \TT{println} prend deux paramètres en entrée:
1)\emph{this}, i.e., un pointeur sur un objet;
2) l'adresse de la chaîne \q{Hello, World}.

En effet, \TT{println()} est appelé comme une méthode dans un objet \TT{System.out}
initialisé.


Par commodité, l'utilitaire \TT{javap} écrit toutes ces informations dans les commentaires.

