% TODO proof-reading
\subsubsection{Fonctions variadiques}

Les fonctions variadiques utilisent en fait des tableaux:

\begin{lstlisting}[style=customjava]
	public static void f(int... values)
	{
		for (int i=0; i<values.length; i++)
			System.out.println(values[i]);
	}

	public static void main(String[] args) 
	{
		f (1,2,3,4,5);
	}
\end{lstlisting}

\begin{lstlisting}
  public static void f(int...);
    flags: ACC_PUBLIC, ACC_STATIC, ACC_VARARGS
    Code:
      stack=3, locals=2, args_size=1
         0: iconst_0      
         1: istore_1      
         2: iload_1       
         3: aload_0       
         4: arraylength   
         5: if_icmpge     23
         8: getstatic     #2       // Field java/lang/System.out:Ljava/io/PrintStream;
        11: aload_0       
        12: iload_1       
        13: iaload        
        14: invokevirtual #3       // Method java/io/PrintStream.println:(I)V
        17: iinc          1, 1
        20: goto          2
        23: return        
\end{lstlisting}

\ttf prend juste un tableau d'entier en utilisant \TT{aload\_0} à l'offset 3.

Puis, il prend la taille du tableau, etc.

\begin{lstlisting}
  public static void main(java.lang.String[]);
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=4, locals=1, args_size=1
         0: iconst_5      
         1: newarray       int
         3: dup           
         4: iconst_0      
         5: iconst_1      
         6: iastore       
         7: dup           
         8: iconst_1      
         9: iconst_2      
        10: iastore       
        11: dup           
        12: iconst_2      
        13: iconst_3      
        14: iastore       
        15: dup           
        16: iconst_3      
        17: iconst_4      
        18: iastore       
        19: dup           
        20: iconst_4      
        21: iconst_5      
        22: iastore       
        23: invokestatic  #4       // Method f:([I)V
        26: return        
\end{lstlisting}

Le tableau est construit dans \main en utilisant l'instruction \TT{newarray}, puis
il est rempli, et \ttf est appelée.

Oh, à propos, l'objet tableau n'est pas détruit à la fin de \main.

\myindex{Garbage collector}
Il n'y a pas du tout de destructeurs en Java, car la JVM a un ramasse miette qui
fait ceci automatiquement, lorsqu'il sent qu'il doit.

Que dire de la méthode \TT{format()}?

Elle prend deux arguments en entrée: une chaîne et un tableau d'objets:

\begin{lstlisting}
	public PrintStream format(String format, Object... args)
\end{lstlisting}
( \url{http://docs.oracle.com/javase/tutorial/java/data/numberformat.html} )

Voyons:

\begin{lstlisting}[style=customjava]
	public static void main(String[] args)
	{
		int i=123;
		double d=123.456;
		System.out.format("int: %d double: %f.%n", i, d);
	}
\end{lstlisting}

\begin{lstlisting}
  public static void main(java.lang.String[]);
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=7, locals=4, args_size=1
         0: bipush        123
         2: istore_1      
         3: ldc2_w        #2         // double 123.456d
         6: dstore_2      
         7: getstatic     #4         // Field java/lang/System.out:Ljava/io/PrintStream;
        10: ldc           #5         // String int: %d double: %f.%n
        12: iconst_2      
        13: anewarray     #6         // class java/lang/Object
        16: dup           
        17: iconst_0      
        18: iload_1       
        19: invokestatic  #7         // Method java/lang/Integer.valueOf:(I)Ljava/lang/Integer;
        22: aastore       
        23: dup           
        24: iconst_1      
        25: dload_2       
        26: invokestatic  #8         // Method java/lang/Double.valueOf:(D)Ljava/lang/Double;
        29: aastore       
        30: invokevirtual #9         // Method java/io/PrintStream.format:(Ljava/lang/String;[Ljava/lang/Object;)Ljava/io/PrintStream;
        33: pop           
        34: return        
\end{lstlisting}

Donc, les valeurs des types \emph{int} et \emph{double} sont d'abord convertis en objets
\TT{Integer} et \TT{Double} en utilisant les méthodes \TT{valueOf}.

La méthode \TT{format()} nécessite un objet de type \TT{Object} en entrée, et comme
\TT{Integer} et \TT{Double} sont dérivées de la classe racine \TT{Object}, ils conviennent
comme éléments du tableau en entrée.

D'un autre côté, un tableau est toujours homogène, i.e., il ne peut pas contenir
d'éléments de types différents, ce qui rend impossible de pousser des valeurs \emph{int}
et \emph{double} dedans.

Un tableau d'objets \TT{Object} est créé à l'offset 13, un objet \TT{Integer} est
ajouté au tableau à l'offset 22, et un objet \TT{Double} est ajouté au tableau à
l'offset 29.

La pénultième instruction \TT{pop} supprime l'élément du \ac{TOS}, donc lorsque
\TT{return} est exécuté, la pile se retrouve vide (ou balancée).

