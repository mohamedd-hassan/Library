% TODO proof-reading
\subsubsection{Exemple simple}

Créons d'abord un tableau de 10 entiers et remplissons le:

\begin{lstlisting}[style=customjava]
	public static void main(String[] args) 
	{
		int a[]=new int[10];
		for (int i=0; i<10; i++)
			a[i]=i;
		dump (a);
	}
\end{lstlisting}

\begin{lstlisting}
  public static void main(java.lang.String[]);
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=3, locals=3, args_size=1
         0: bipush        10
         2: newarray       int
         4: astore_1      
         5: iconst_0      
         6: istore_2      
         7: iload_2       
         8: bipush        10
        10: if_icmpge     23
        13: aload_1       
        14: iload_2       
        15: iload_2       
        16: iastore       
        17: iinc          2, 1
        20: goto          7
        23: aload_1       
        24: invokestatic  #4     // Method dump:([I)V
        27: return        
\end{lstlisting}

L'instruction \TT{newarray} créée un objet tableau de 10 éléments de type \emph{int}.

La taille du tableau est définie par \TT{bipush} et laissée sur le \ac{TOS}.

Le type du tableau est mis dans l'opérande de l'instruction \TT{newarray}.

Après l'exécution de \TT{newarray}, une \emph{référence} (ou pointeur) sur le tableau
nouvellement créé dans le heap est laissée sur le \ac{TOS}.

\TT{astore\_1} stocke la \emph{référence} dans le 1er slot dans \ac{LVA}.

La seconde partie de la fonction \main est la boucle qui stocke \emph{i} dans l'élément
du tableau correspondant.

\TT{aload\_1} obtient une \emph{référence} du tableau et la met sur la pile.

\TT{iastore} stocke ensuite la valeur entière de la pile dans le tableau, dont la
\emph{référence} se trouve dans \ac{TOS}.

La troisième partie de la fonction \main appelle la fonction \TT{dump()}.

Un argument lui est préparé par \TT{aload\_1} (offset 23).

Maintenant regardons la fonction \TT{dump()}:

\begin{lstlisting}[style=customjava]
	public static void dump(int a[])
	{
		for (int i=0; i<a.length; i++)
			System.out.println(a[i]);
	}
\end{lstlisting}

\begin{lstlisting}
  public static void dump(int[]);
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=3, locals=2, args_size=1
         0: iconst_0      
         1: istore_1      
         2: iload_1       
         3: aload_0       
         4: arraylength   
         5: if_icmpge     23
         8: getstatic     #2      // Field java/lang/System.out:Ljava/io/PrintStream;
        11: aload_0       
        12: iload_1       
        13: iaload        
        14: invokevirtual #3      // Method java/io/PrintStream.println:(I)V
        17: iinc          1, 1
        20: goto          2
        23: return        
\end{lstlisting}

La \TT{référence} entrante sur le tableau est dans le slot d'indice 0.

L'expression \TT{a.length} dans le code source est convertie en une instruction \TT{arraylength}:
elle prend une \TT{référence} sur le tableau et laisse sa taille sur le \ac{TOS}.

\TT{iaload} à l'offset 13 est utilisée pour charger des éléments du tableau,
elle nécessite qu'une \emph{référence} sur le tableau soit présente
dans la pile (préparée par \TT{aload\_0} en 11),
et aussi un index (préparé par \TT{iload\_1} à l'offset 12).

Inutile de dire que les instructions préfixées par \emph{a} peuvent être, par erreur,
mal interprétées comme des instructions d'\emph{array} (tableaux).

C'est incorrect.
Ces instructions travaillent avec des \emph{références} sur les objets.

Et les tableaux et les chaînes sont aussi des objets.

