% TODO proof-reading
\subsection{Semplici funzioni di calcolo}

Continuiamo con delle semplici funzioni di calcolo.

\begin{lstlisting}[style=customjava]
public class calc
{
	public static int half(int a)
	{
		return a/2;
	}
}
\end{lstlisting}

Ecco il risultato, quando ad essere impiegata è l'istruzione \INS{iconst\_2}:


\begin{lstlisting}
  public static int half(int);
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=2, locals=1, args_size=1
         0: iload_0       
         1: iconst_2      
         2: idiv          
         3: ireturn       
\end{lstlisting}
         
\INS{iload\_0} prende il primo argomento passato alla funzione (il dato nella posizione 0) e lo carica nello stack.

\INS{iconst\_2} carica nello stack il valore 2.
Al termine dell'esecuzione di queste due istruzioni, lo stato dello stack è il seguente:


% FIXME: TikZ
\begin{lstlisting}
      +---+
TOS ->| 2 |
      +---+
      | a |
      +---+
\end{lstlisting}

\INS{idiv} prende i primi due valori in testa allo stack \ac{TOS}, divide uno per l'altro e carica il risultato 
in testa allo stack \ac{TOS}:


% FIXME: TikZ
\begin{lstlisting}
      +--------+
TOS ->| result |
      +--------+
\end{lstlisting}

\INS{ireturn} ritorna indietro il primo valore trovato in testa allo stack.

Procediamo ora con numeri in virgola mobile e precisione doppia:


\begin{lstlisting}[style=customjava]
public class calc
{
	public static double half_double(double a)
	{
		return a/2.0;
	}
}
\end{lstlisting}

\begin{lstlisting}[caption=Constant pool]
...
   #2 = Double             2.0d
...
\end{lstlisting}

\begin{lstlisting}
  public static double half_double(double);
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=4, locals=2, args_size=1
         0: dload_0       
         1: ldc2_w        #2                  // double 2.0d
         4: ddiv          
         5: dreturn       
\end{lstlisting}

Il risultato delle decompilazione è simile al caso precedente, ma qui viene utilizzata l'istruzione \INS{ldc2\_w} per caricare nello stack
il valore 2.0, a sua volta preso dal \q{constant pool}.

Inoltre, le altre tre istruzioni hanno il prefisso \emph{d}, 
ad indicare come esse lavorino con il tipo \emph{double}.


Vediamo adesso delle funzioni con due argomenti:


\begin{lstlisting}[style=customjava]
public class calc
{
	public static int sum(int a, int b)
	{
		return a+b;
	}
}
\end{lstlisting}

\begin{lstlisting}
  public static int sum(int, int);
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=2, locals=2, args_size=2
         0: iload_0       
         1: iload_1       
         2: iadd          
         3: ireturn       
\end{lstlisting}

\INS{iload\_0} carica il primo argomento (a), \INS{iload\_1}---il secondo (b).

Di seguito lo stato dello stack al termine di entrambe le istruzioni:


\begin{lstlisting}
      +---+
TOS ->| b |
      +---+
      | a |
      +---+
\end{lstlisting}

\INS{iadd} somma i due valori e carica il risultato in testa allo stack \ac{TOS}:


\begin{lstlisting}
      +--------+
TOS ->| result |
      +--------+
\end{lstlisting}

Estendiamo l'esempio al tipo di dato \emph{long}:


\begin{lstlisting}[style=customjava]
	public static long lsum(long a, long b)
	{
		return a+b;
	}
\end{lstlisting}

\dots abbiamo:

\begin{lstlisting}
  public static long lsum(long, long);
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=4, locals=4, args_size=2
         0: lload_0       
         1: lload_2       
         2: ladd          
         3: lreturn       
\end{lstlisting}

La seconda istruzione \TT{lload} prende l'argomento che si trova nello spazio dati in posizione 2.

Ciò accade poiché il tipo \emph{long} occupa 64-bit, ossia due spazi da 32-bit,
per cui la posizione del secondo argomento è 2 e non 1.


Ecco un esempio leggermente più complesso:


\begin{lstlisting}[style=customjava]
public class calc
{
	public static int mult_add(int a, int b, int c)
	{
		return a*b+c;
	}
}
\end{lstlisting}

\begin{lstlisting}
  public static int mult_add(int, int, int);
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=2, locals=3, args_size=3
         0: iload_0       
         1: iload_1       
         2: imul          
         3: iload_2       
         4: iadd          
         5: ireturn       
\end{lstlisting}

Il primo passo è una moltiplicazione. Il prodotto viene caricato in testa allo stack \ac{TOS}:


\begin{lstlisting}
      +---------+
TOS ->| product |
      +---------+
\end{lstlisting}

\TT{iload\_2} carica il terzo argomento (c) nello stack:

\begin{lstlisting}
      +---------+
TOS ->|    c    |
      +---------+
      | product |
      +---------+
\end{lstlisting}

L'istruzione \TT{iadd} somma i due valori prelevati dalla testa dello stack.

